module HeronToXML
{
    imports
    {
        console = new Heron.Windows.Console();
    }
    fields
    {
        indent : String;
        atNewLine : Bool;
    }
    methods
    {        
        Constructor()
        {
            indent = new String();
            atNewLine = false;
        }
        Output(program : ProgramDefn)
        {
            foreach (m in program.GetModules())
                Output(m);
        }
        OutputRaw(s : String)
        {
            if (atNewLine)
            {
                atNewLine = false;
                WriteLine();
                Write(indent);
            } 
            Write(s);
        }
        Output(s : String)
        {
            s = Regex.Replace(s, "&", "&amp;");
            s = Regex.Replace(s, "<", "&lt;");
            OutputRaw(s);
        }
        OutputLine()
        {
            OutputLine("");
        }            
        OutputLine(s : String)
        {
            Output(s);
            atNewLine = true;
        }
        Indent(f : Function)
        {
            var old_indent = indent;
            indent = indent + "  ";
            f();
            indent = old_indent;
        }
        OpenTag(tag : String)
        {
            OutputRaw("<" + tag +">");
        }
        OpenNamedTag(tag : String, name : String)
        {
            OutputRaw("<" + tag +" name='" + name + "'>\n");
        }
        CloseTag(tag : String)
        {
            OutputRaw("</" + tag + ">\n");
        }
        OutputTaggedFunction(tag : String, f : Function)
        {
            OpenTag(tag);
            Indent(f);
            CloseTag(tag);
        }            
        OutputList(tag : String, xs : Seq)
        {
            OpenTag(tag);
            OutputAll(xs);
            CloseTag(tag);
        }
        OutputAll(xs : Seq)
        {
            foreach (x in xs)
                Output(x);
        }
        OutputTagged(tag : String, x)
        {
            OpenTag(tag);
            Output(x);
            CloseTag(tag);
        }
        OutputEmptyTag(tag : String)
        {
            OutputRaw("<" + tag + "/>");
        }
        OutputName(s : String)
        {
            OutputTagged("name", s);
        }
        OutputType(t : TypeType)
        {
            var s : String = t.name;
            OutputTagged("type", s);
        }
        Output(m : ModuleDefn)
        {
            OpenTag("module");
            OutputName(m.name);
            OutputSection("imports", m.GetImports());
            OutputSection("fields", m.GetFields());
            OutputSection("methods", m.GetDeclaredMethods());
            OpenTag("types");
            OutputAll(m.GetClasses());
            OutputAll(m.GetInterfaces());
            OutputAll(m.GetEnums());
            CloseTag("types");
            CloseTag("module");
        }
        OutputSection(name : String, xs : Seq)
        {
            OpenTag(name);
            OutputAll(xs);
            CloseTag(name);
        }
        Output(c : ClassDefn)
        {
            OpenTag("class");
            OutputName(c.name);
            OutputSection("inherits", c.GetInheritedClasses());
            OutputSection("implements", c.GetImplementedInterfaces());
            OutputSection("fields", c.GetFields());
            OutputSection("methods", c.GetDeclaredMethods());
            CloseTag("class");
        }
        Output(i : InterfaceDefn)
        {
            OpenTag("interface");
            OutputName(i.name);
            OutputSection("inherits", i.GetInheritedInterfaces());
            OutputSection("methods", i.GetDeclaredMethods());
            CloseTag("interface");
        }
        Output(e : EnumDefn)
        {
            OpenTag("enum");
            OutputName(e.name);
            foreach (v in e.GetValues())
                OutputTagged("value", v.GetValue());
            CloseTag("enum");
        }
        OutputVarDesc(v : VarDesc)
        {
            OutputName(v.name);
            OutputType(v.type);
            if (v.nullable)
                OutputEmptyTag("nullable");
        }
        Output(f : FieldDefn)
        {
            OpenTag("field");
            OutputVarDesc(f);
            if (f.expr != null)
                Output(f.expr);
            CloseTag("field");
        }
        Output(m : FunctionDefn)
        {
            OpenTag("function");
            OutputName(m.name);
            OutputList("parameters", m.formals);
            OutputType(m.rettype);
            // OutputTagged("body", m.body);
            CloseTag("function");
        }
        Output(i : Import)
        {
            OpenTag("import");
            OutputTagged("alias", i.alias);
            OutputTagged("modulename", i.module);
            OutputList("args", i.args);
            CloseTag("import");
        }
        Output(fa : FormalArg)
        {
            OpenTag("param");
            OutputVarDesc(fa);
            CloseTag("param");
        }
        Output(x : VariableDeclaration)
        {
            OpenTag("vardecl");
            OutputVarDesc(x.vardesc);
            if (x.value != null)
                Output(x.value);
            CloseTag("vardecl");
        }
        Output(x : DeleteStatement)
        {
            OpenTag("delete");
            Output(x.expression);
            CloseTag("delete");
        }
        Output(x : ExpressionStatement)
        {
            OpenTag("exprstatement");
            Output(x.expression); 
            CloseTag("exprstatement");
        }
        Output(x : ForEachStatement)
        {
            OpenTag("foreach");
            OutputName(x.name);
            OutputType(x.type);
            OutputTagged("collection", x.collection);
            OutputTagged("body", x.body);
            CloseTag("foreach");
        }
        Output(x : ForStatement)
        {
            OpenTag("for");
            OutputName(x.name);
            OutputType(x.type);
            OutputTagged("initial", x.initial);
            OutputTagged("condition", x.condition);
            OutputTagged("next", x.next);
            OutputTagged("collection", x.collection);
            OutputTagged("body", x.body);
            CloseTag("for");
        }
        Output(x : CodeBlock)
        {
            OutputList("block", x.statements);
        }
        Output(x : IfStatement)
        {
            OpenTag("if");
            OutputTagged("condition", x.condition);
            OutputTagged("ontrue", x.ontrue);
            if (x.onfalse != null)
                OutputTagged("onfalse", x.onfalse);
            CloseTag("if");
        }
        Output(x : WhileStatement)
        {
            OpenTag("while");
            OutputTagged("condition", x.condition);
            OutputTagged("body", x.body);
            CloseTag("while");
        }
        Output(x : ReturnStatement)
        {
            OpenTag("return");
            if (x.expression != null)
                OutputTagged("value", x.expression);
            CloseTag("return");
        }
        Output(x : SwitchStatement)
        {
            OpenTag("switch");
            OutputTagged("condition", x.condition);
            OutputLists("cases", x.cases);
            if (x.ondefault != null)
                OutputTagged("default", x.ondefault);
            CloseTag("switch");
        }
        Output(x : CaseStatement)
        {
            OpenTag("case");
            Output(x.condition);
            Output(x.statement);
            CloseTag("case");
        }
        Output(x : Assignment)
        {
            OpenTag("assignment");
            OutputTagged("lvalue", x.lvalue);
            OutputTagged("rvalue", x.rvalue);
            CloseTag("assignment");
        }
        Output(x : ChooseField)
        {
            OpenTag("readfield");
            OutputTagged("self", x.self);
            OutputTagged("name", x.name);
            CloseTag("readfield");
        }
        Output(x : ReadAt)
        {
            OpenTag("readat");
            OutputTagged("self", x.self);
            OutputTagged("index", x.index);
            CloseTag("readat");
        }
        Output(x : NewExpr)
        {
            OpenTag("new");
            OutputType(x.type);
            OutputList("args", x.args);
            if (x.module != null)
                OutputTagged("srcmodule", x.module); 
            CloseTag("new");
        }
        Output(x : NullExpr)
        {
            EmptyTag("null");            
        }
        OutputValue(tag : String, value : String)
        {
            OutputRaw("<" + tag + " value='" + value + "'/>");
        }
        Output(x : IntLiteral)
        {
            OutputValue("int", x.ToString());
        }
        Output(x : BoolLiteral)
        {
            OutputValue("bool", x.ToString());
        }
        Output(x : FloatLiteral)
        {
            OutputValue("float", x.ToString());
        }
        Output(x : CharLiteral)
        {
            OutputValue("char", x.ToString());
        }
        Output(x : StringLiteral)
        {
            OutputValue("string", x.ToString());
        }
        Output(x : Name)
        {
            OutputValue("name", x.ToString());
        }
        Output(x : FunCall)
        {
            OpenTag("funcall");
            OutputTagged("function", x.funexpr);
            OutputList("args", x.args);
            CloseTag("funcall");
        }
        Output(x : UnaryOperation)
        {
            OpenTag("unaryop");
            OutputTagged("operator", x.operation);
            OutputTagged("operand", x.operand);
            CloseTag("unaryop");
        }
        Output(x : BinaryOperation)
        {
            OpenTag("binaryop");
            OutputTagged("operator", x.operation);
            OutputTagged("loperand", x.operand1);
            OutputTagged("roperand", x.operand2);
            CloseTag("binaryop");
        }
        Output(x : FunExpr)
        {
            OpenTag("closure");
            OutputList("params", x.formals);
            OutputType(x.rettype);
            OutputTagged("body", x.body);
            CloseTag("closure");
        }
        Output(x : PostIncExpr)
        {
            OutputTagged("postinc", x.expr);
        }
        Output(x : SelectExpr)
        {
            OpenTag("select");
            OutputTagged("name", x.name);
            OutputTagged("list", x.list);
            OutputTagged("predicate", x.predicate);
            CloseTag("select");
        }
        Output(x : MapExpr)
        {
            OpenTag("map");
            OutputTagged("name", x.name);
            OutputTagged("list", x.list);
            OutputTagged("yield", x.yield);
            CloseTag("map");
        }
        Output(x : AccumulateExpr)
        {
            OpenTag("accumulate");
            OutputTagged("accumulator", x.acc);
            OutputTagged("initializer", x.init);
            OutputTagged("each", x.each);
            OutputTagged("list", x.list);
            OutputTagged("expression", x.expr);
            CloseTag("accumulate");
        }
        Output(x : TupleExpr)
        {
            OutputList("tuple", x.exprs);
        }
        Output(x : ParanthesizedExpr)
        {
            OutputTagged("paranthesized", x.expr);
        }
        Output(x : TableExpr)
        {
            OpenTag("table");
            OutputFormalArgs(x.fielddefs);
            OutputList("rows", x.rows);
            CloseTag("table");
        }
        Output(x : RecordExpr)
        {
            OpenTag("record");
            OutputFormalArgs(x.fielddefs);
            OutputList("fields", x.fields);
            CloseTag("record");
        }
    }
}

